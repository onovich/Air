#pragma kernel CSMain
static const int threadGroupSize = 1024;

struct Boid {
    float3 position;
    float3 direction;

    float3 alignment;
    float3 cohesionCenter;
    float3 separation;

    int cohesionCount;
};

RWStructuredBuffer<Boid> boids;
uint boidsCount;
float viewRadius;
float avoidRadius;

[numthreads(threadGroupSize,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {
    uint indexA = id.x;
    Boid boidA = boids[indexA];
    boids[indexA].alignment = float3(0, 0, 0);
    boids[indexA].cohesionCenter = float3(0, 0, 0);
    boids[indexA].separation = float3(0, 0, 0);
    boids[indexA].cohesionCount = 0;

    for (uint indexB = 0; indexB < boidsCount; indexB++) {
        if (indexA != indexB) {
            Boid boidB = boids[indexB];
            float3 offset = boidB.position - boidA.position;
            float sqrDst = offset.x * offset.x + offset.y * offset.y; 

            if (sqrDst < viewRadius * viewRadius) {
                boids[indexA].cohesionCount += 1;
                // 对齐 Boids
                boids[indexA].alignment += boidB.direction;
                // 聚集 Boids
                boids[indexA].cohesionCenter += boidB.position;

                // 分离 Boids
                if (sqrDst < avoidRadius * avoidRadius) {
                    boids[indexA].separation -= offset / sqrt(sqrDst);
                }
            }
        }
    }
}